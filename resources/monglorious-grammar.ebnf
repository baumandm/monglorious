(* MONGLORIOUS! *)
query = show-command | run-command | collection-command

show-command = <'SHOW'> db-object
db-object = dbs | collections | users | roles | profile | databases
dbs = <'DBS'>
collections = <'COLLECTIONS'>
users = <'USERS'>
roles = <'ROLES'>
profile = <'PROFILE'>
databases = <'DATABASES'>

collection-command = DB DOT collection-name (DOT function-application)+
<collection-name> = identifier
function-application = function-name LPAREN (literal (COMMA literal)*)? RPAREN
<function-name> = identifier

run-command = DB DOT <'RUNCOMMAND'> LPAREN literal RPAREN

(* Common Definitions *)
identifier = #'\$?[a-zA-Z0-9_]+'
<literal> = string | number | map | list | boolean | objectid | regex | date
<string> = single-quoted-string | double-quoted-string
single-quoted-string = #'(?s)\'\'|\'(?:.*?([^\\]|\\\\))?\''
double-quoted-string = #'(?s)\"\"|\"(?:.*?([^\\]|\\\\))\"'
number = #'[-+]?(0(\.\d*)?|([1-9]\d*\.?\d*)|(\.\d+))([Ee][+-]?\d+)?'
list = <'['> (literal (COMMA literal)*)? <']'>
map = <'{'> (map-item (COMMA map-item)*)? <'}'>
<map-item> = (identifier | string) <':'> literal
boolean = TRUE | FALSE
objectid = <'objectid'> LPAREN string RPAREN
regex = <'/'> regex-contents <'/'> regex-options?
<regex-contents> = #'[^//]+'
<regex-options> = #'[imxs]+'
<date> = date-string | new-date | isodate
date-string = <'Date'> LPAREN <(literal (COMMA literal)*)?> RPAREN
new-date = NEW <'Date'> LPAREN <(literal (COMMA literal)*)?> RPAREN
isodate = NEW? <'ISODate'> LPAREN (literal (COMMA literal)*)? RPAREN

<DB> = <'DB'>
<DOT> = <'.'>
<COMMA> = <','>
<LPAREN> = <'('>
<RPAREN> = <')'>
TRUE = <'TRUE'>
FALSE = <'FALSE'>
<NEW> = <'NEW'>
